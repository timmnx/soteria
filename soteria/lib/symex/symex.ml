(** The core of Soteria symbolic execution. *)

open Soteria_std
open Logs.Import
open Syntaxes.FunctionWrap

(* Re-export a few definitions, leaving this module as root. *)
module Approx = Approx
module Fuel_gauge = Fuel_gauge
module Solver = Solver
module Solver_result = Solver_result
module Value = Value
module Var = Var

exception Tool_bug of string
exception Gave_up of string

let tool_bug reason = raise (Tool_bug reason)

let () =
  Printexc.register_printer (function
    | Gave_up reason ->
        Some (Fmt.str "Analysis gave up (and was not caught): %s" reason)
    | Tool_bug reason ->
        Some
          (Fmt.str
             "@[<v>Tool Bug : %s@.This is due to an invalid use of the Soteria \
              API, please report this with the tool developer.@]"
             reason)
    | _ -> None)

(** The different ways a managed effect can be handled. *)
type 'a effect_handling =
  | Ignore
      (** Handle the effect by ignoring it, so don't collect any information. *)
  | Dump of 'a
      (** Handle the effect, and dump the information according to the relevant
          configuration. The value is the parameter passed to the feature's dump
          handler. *)
  | Caller
      (** The effect is already handled by the caller, nothing needs to be done.
      *)

module Or_gave_up = struct
  type 'err t = E of 'err | Gave_up of string

  let pp pp_err fmt = function
    | E e -> pp_err fmt e
    | Gave_up reason -> Format.fprintf fmt "Gave up: %s" reason

  let unwrap_exn = function
    | E e -> e
    | Gave_up reason -> raise (Gave_up reason)
end

module type Symex_syntax_S = sig
  type sbool_v
  type ('a, 'b) t

  val branch_on :
    ?left_branch_name:string ->
    ?right_branch_name:string ->
    sbool_v ->
    then_:(unit -> ('a, 'b) t) ->
    else_:(unit -> ('a, 'b) t) ->
    ('a, 'b) t

  val branch_on_take_one :
    ?left_branch_name:string ->
    ?right_branch_name:string ->
    sbool_v ->
    then_:(unit -> ('a, 'b) t) ->
    else_:(unit -> ('a, 'b) t) ->
    ('a, 'b) t

  val if_sure :
    ?left_branch_name:string ->
    ?right_branch_name:string ->
    sbool_v ->
    then_:(unit -> ('a, 'b) t) ->
    else_:(unit -> ('a, 'b) t) ->
    ('a, 'b) t
end

module type Core = sig
  module Value : Value.S

  (** Represents a yet-to-be-executed symbolic process which terminates with a
      value of type ['a]. *)
  type 'a t

  include Monad.Base with type 'a t := 'a t

  (** Type of error that corresponds to a logical failure (i.e. a logical
      mismatch during consumption).

      Use this instead of [`Lfail] directly in type signatures to avoid
      potential typos such as [`LFail] which will take precious time to debug...
      trust me. *)
  type lfail = [ `Lfail of Value.(sbool t) ]
  [@@deriving show { with_path = false }]

  type cons_fail = [ lfail | `Missing_subst of Var.t ]
  [@@deriving show { with_path = false }]

  type 'a v := 'a Value.t
  type 'a vt := 'a Value.ty
  type sbool := Value.sbool

  val assume : sbool v list -> unit t
  val vanish : unit -> 'a t

  (** Assert is a symbolic process that does not branch but tests for the
      feasibility of the input symbolic value.

      - In UX, [assert_] returns [false] if and only if [not value] is
        {b satisfiable}.
      - In OX, [assert_] returns [true] if and only if [not value] is
        {b unsatisfiable}. *)
  val assert_ : sbool v -> bool t

  (** Do not use [nondet_UNSAFE]. *)
  val nondet_UNSAFE : 'a vt -> 'a v
  (* [nondet_UNSAFE] creates a nondet value but does not wrap it inside a symex
     monad. This could be used unsafely, because it's not lazy. It is exposed
     because we use it in Producer. TODO: this may be removable when we have
     modular explicit, and we can thread a monad through all our definitions *)

  (** [nondet ty] creates a fresh variable of type [ty]. *)
  val nondet : 'a vt -> 'a v t

  (** [simplify v] simplifies the value [v] according to the current path
      condition. *)
  val simplify : 'a v -> 'a v t

  val fresh_var : 'a vt -> Var.t t

  val branch_on :
    ?left_branch_name:string ->
    ?right_branch_name:string ->
    sbool v ->
    then_:(unit -> 'a t) ->
    else_:(unit -> 'a t) ->
    'a t

  (** [if_sure cond ~then_ ~else_] evaluates the [~then_] branch if [cond] is
      guaranteed to hold in the current context, and otherwise evaluates
      [~else_].

      This is to be used with caution: the [~then_] branch should {e always}
      describe a behaviour that is semantically equivalent to that of the
      [~else_] branch when [cond] holds. *)
  val if_sure :
    ?left_branch_name:string ->
    ?right_branch_name:string ->
    sbool v ->
    then_:(unit -> 'a t) ->
    else_:(unit -> 'a t) ->
    'a t

  (** Branches on value, and ({b in UX only}) takes at most one branch, starting
      with the [then] branch. This means that if the [then_] branch is SAT, it
      is taken and the [else_] branch is ignored, otherwise the [else_] branch
      is taken. In OX mode, this behaves exactly as [branch_on]. *)
  val branch_on_take_one :
    ?left_branch_name:string ->
    ?right_branch_name:string ->
    sbool v ->
    then_:(unit -> 'a t) ->
    else_:(unit -> 'a t) ->
    'a t

  (** Gives up on this path of execution for incompleteness reason. For
      instance, if a give feature is unsupported. *)
  val give_up : string -> 'a t

  (** Runs the process within a section of execution with given name.
      Corresponds to frames in the flamegraph. *)
  val with_frame : string -> (unit -> 'a t) -> 'a t

  val branches : (unit -> 'a t) list -> 'a t

  (** {2 Fuel} *)

  val consume_fuel_steps : int -> unit t
end

module type Base = sig
  include Core
  include Monad.S with type 'a t := 'a t

  (** [assert_or_error guard err] asserts [guard] is true, and otherwise returns
      [Compo_res.Error err]. Biased towards the assertion being [false] to
      reduce SAT-checks.

      This is provided as a utility, and is equivalent to
      {@ocaml[
      branch_on (not guard)
        ~then_:(fun () -> return (Compo_res.error err))
        ~else_:(fun () -> return (Compo_res.ok ()))
      ]} *)
  val assert_or_error :
    Value.(sbool t) -> 'err -> (unit, 'err, 'f) Compo_res.t t

  (** If the given option is None, gives up execution, otherwise continues,
      unwrapping the option. *)
  val some_or_give_up : string -> 'a option -> 'a t

  module Result : sig
    include
      Compo_res.S
        with type ('ok, 'err, 'fix) t = ('ok, 'err, 'fix) Compo_res.t t

    (** Missing without any fix. Will add to the statistics and log that
        information. *)
    val miss_no_fix : reason:string -> unit -> ('ok, 'err, 'fix) t
  end

  module Syntax : sig
    include Monad.Syntax with type 'a t := 'a t

    include
      Compo_res.Syntax
        with type ('ok, 'err, 'fix) t := ('ok, 'err, 'fix) Result.t

    module Symex_syntax :
      Symex_syntax_S
        with type ('a, 'b) t := 'a t
         and type sbool_v := Value.(sbool t)
  end

  module Producer : sig
    type 'a symex := 'a t
    type subst := Value.Expr.Subst.t

    include Monad.S

    val lift : 'a symex -> 'a t
    val vanish : unit -> 'a t

    val apply_subst :
      ((Value.Expr.t -> 'a Value.t) -> 'syn -> 'sem) -> 'syn -> 'sem t

    val produce_pure : Value.Expr.t -> unit t
    val run : subst:subst -> 'a t -> ('a * subst) symex
    val run_identity : 'a t -> 'a symex

    (** This is unsafe and shouldn't be used in clients, it is only available to
        enable the implementation of the state monad transformer. *)
    val from_raw_UNSAFE : (subst option -> ('a * subst option) symex) -> 'a t

    module Syntax : sig
      include module type of Syntax

      val ( let*^ ) : 'a symex -> ('a -> 'b t) -> 'b t
      val ( let+^ ) : 'a symex -> ('a -> 'b) -> 'b t

      module Symex_syntax :
        Symex_syntax_S
          with type ('a, 'b) t := 'a t
           and type sbool_v := Value.(sbool t)
    end
  end

  module Consumer : sig
    type subst := Value.Expr.Subst.t
    type 'a symex := 'a t
    type ('a, 'fix) t

    include Monad.Extension2 with type ('a, 'fix) t := ('a, 'fix) t

    val apply_subst :
      ((Value.Expr.t -> 'a Value.t) -> 'syn -> 'sem) -> 'syn -> ('sem, 'fix) t

    val assert_pure : Value.(sbool t) -> (unit, 'fix) t
    val consume_pure : Value.Expr.t -> (unit, 'fix) t
    val learn_eq : Value.Expr.t -> 'a Value.t -> (unit, 'fix) t
    val expose_subst : unit -> (subst, 'fix) t
    val lift_res : ('a, cons_fail, 'fix) Result.t -> ('a, 'fix) t
    val lift : 'a symex -> ('a, 'fix) t
    val branches : (unit -> ('a, 'fix) t) list -> ('a, 'fix) t
    val ok : 'a -> ('a, 'fix) t
    val lfail : Value.sbool Value.t -> ('a, 'fix) t
    val miss : 'fix list -> ('a, 'fix) t
    val miss_no_fix : reason:string -> unit -> ('a, 'fix) t
    val map : ('a -> 'b) -> ('a, 'fix) t -> ('b, 'fix) t
    val map_missing : ('fix -> 'g) -> ('a, 'fix) t -> ('a, 'g) t
    val bind : ('a -> ('b, 'fix) t) -> ('a, 'fix) t -> ('b, 'fix) t

    val bind_res :
      (('a, cons_fail, 'fix) Compo_res.t -> ('b, 'fix2) t) ->
      ('a, 'fix) t ->
      ('b, 'fix2) t

    val run :
      subst:subst -> ('a, 'fix) t -> ('a * subst, cons_fail, 'fix) Result.t

    (** This is unsafe and shouldn't be used in clients, it is only available to
        enable the implementation of the state monad transformer. *)
    val from_raw_UNSAFE :
      (subst -> ('a * subst, cons_fail, 'fix) Result.t) -> ('a, 'fix) t

    module Syntax : sig
      val ( let* ) : ('a, 'fix) t -> ('a -> ('b, 'fix) t) -> ('b, 'fix) t
      val ( let+ ) : ('a, 'fix) t -> ('a -> 'b) -> ('b, 'fix) t
      val ( let+? ) : ('a, 'fix) t -> ('fix -> 'g) -> ('a, 'g) t

      val ( let*! ) :
        ('a, 'fix) t ->
        (('a, cons_fail, 'fix) Compo_res.t -> ('b, 'fix2) t) ->
        ('b, 'fix2) t

      val ( let*^ ) : 'a symex -> ('a -> ('b, 'fix) t) -> ('b, 'fix) t
      val ( let+^ ) : 'a symex -> ('a -> 'b) -> ('b, 'fix) t

      module Symex_syntax :
        Symex_syntax_S
          with type ('a, 'b) t := ('a, 'b) t
           and type sbool_v := Value.(sbool t)
    end
  end
end

module type S = sig
  include Base

  (** A Symex runs in a pooled solver environment. This module exposes some
      information about the solver pool. *)
  module Solver_pool : sig
    val total_created : unit -> int
    val total_available : unit -> int
  end

  (** [run ~mode p] actually performs symbolic execution of the symbolic process
      [p] and returns a list of obtained branches which capture the outcome
      together with a path condition that is a list of boolean symbolic values.

      The [mode] parameter is used to specify whether execution should be done
      in an under-approximate ({!Symex.Approx.UX}) or an over-approximate
      ({!Symex.Approx.OX}) manner. Users may optionally pass a
      {{!Fuel_gauge.t}fuel gauge} to limit execution depth and breadth.

      This function also takes a number of optional parameters to configure how
      the execution is performed and what information is collected during
      execution:
      - [fuel] receives a {{!Fuel_gauge.t}fuel gauge} to limit execution depth
        and breadth. By default, the fuel is infinite.
      - [stats] specifies whether statistics about the execution are already
        handled outside the function (see {!Stats.As_ctx.with_}) or should be
        handled and ignored by this function (default).
      - [flamegraph] specifies whether a flamegraph should be created from this
        symbolic process or if it should be ignored (default). The value passed
        in the {{!effect_handling.Dump}[Dump]} variant is the name of the
        created file (while the {{!Profiling.Config}configuration} for
        flamegraphs specifies the directory in which flamegraphs are saved).

      @raise Symex.Gave_up
        if the symbolic process calls [give_up] and the mode is
        {!Symex.Approx.OX}. Prefer using {!Result.run} when possible. *)
  val run :
    ?flamegraph:string effect_handling ->
    ?stats:unit effect_handling ->
    ?fuel:Fuel_gauge.t ->
    mode:Approx.t ->
    'a t ->
    ('a * Value.Expr.t list) list

  module Result : sig
    include module type of Result

    (** Same as {{!Symex.S.run}[run]}, but receives a symbolic process that
        returns a {{!Soteria_std.Compo_res.t}[Compo_res.t]} and maps the result
        to an {{!Symex.Or_gave_up.t}[Or_gave_up.t]}, potentially adding any path
        that gave up to the list. *)
    val run :
      ?flamegraph:string effect_handling ->
      ?stats:unit effect_handling ->
      ?fuel:Fuel_gauge.t ->
      ?fail_fast:bool ->
      mode:Approx.t ->
      ('ok, 'err, 'fix) t ->
      (('ok, 'err Or_gave_up.t, 'fix) Compo_res.t * Value.Expr.t list) list
  end
end

module StatKeys = struct
  (** Keys for statistics used in Soteria's symex engine. These are exposed so
      clients can query statistics if needed; the type with which they are
      logged is also documented.

      It is recommended for clients to not use these keys for custom statistics
      tracking. To avoid clashes, all of these are prefixed with [soteria.] *)

  (** Total execution time. Logged as a {!Stats.Float}. *)
  let exec_time = "soteria.exec-time"

  (** SAT solving time. Logged as a {!Stats.Float}. *)
  let sat_time = "soteria.sat-time"

  (** Number of calls to the solver's [sat] function. Logged as a {!Stats.Int}.
  *)
  let sat_checks = "soteria.sat-checks"

  (** Number of calls to the solver's [sat] function that returned [Unknown].
      Logged as a {!Stats.Int}. *)
  let sat_unknowns = "soteria.sat-unknowns"

  (** Number of unexplored branches due to fuel exhaustion. Logged as a
      {!Stats.Int}. *)
  let unexplored_branches = "soteria.unexplored-branches"

  (** Number of branches explored. Logged as a {!Stats.Int}. *)
  let branches = "soteria.branches"

  (** Total number of steps taken across all branches. Logged as a {!Stats.Int}.
  *)
  let steps = "soteria.steps"

  (** Number of give-ups due to incompleteness. Logged as a
      {!Stats.stat_entry.StrSeq}. *)
  let give_up_reasons = "soteria.give-up-reasons"

  (** Number of misses without any fix. Logged as a {!Stats.stat_entry.StrSeq}.
  *)
  let miss_without_fix = "soteria.miss-without-fix"

  (** Number of times [branch_on] was called *)
  let branch_on_calls = "soteria.branch-on-calls"

  (** Number of times [branch_on] actually branched *)
  let branch_on_branched = "soteria.branch-on-branched"

  let () =
    let open Stats in
    let open Logs.Printers in
    register_float_printer exec_time ~name:"Execution time" (fun _ -> pp_time);
    register_float_printer sat_time ~name:"SAT solving time" (fun stats ft t ->
        let exec_time = get_float stats exec_time in
        Fmt.pf ft "%a (%a)" pp_time t
          (pp_unstable ~name:"%" pp_percent)
          (exec_time, t));
    disable_printer sat_unknowns;
    register_int_printer sat_checks ~name:"SAT checks" (fun stats ft sats ->
        let unknowns = get_int stats sat_unknowns in
        Fmt.pf ft "%d (%d unknowns)" sats unknowns);
    disable_printer unexplored_branches;
    register_int_printer branches ~name:"Branches" (fun stats ft b ->
        let unexplored = get_int stats unexplored_branches in
        Fmt.pf ft "%d (%d unexplored)" b unexplored);
    register_int_printer steps ~name:"Steps" (fun _ -> Fmt.int);
    register_printer give_up_reasons ~name:"Give up reasons" (fun _ ->
        default_printer);
    register_printer miss_without_fix ~name:"Misses without fix" (fun _ ->
        default_printer);
    disable_printer branch_on_branched;
    register_int_printer branch_on_calls ~name:"branch_on"
      (fun stats ft calls ->
        let branched = get_int stats branch_on_branched in
        Fmt.pf ft "branches %a of calls (%d of %d)" pp_percenti
          (calls, branched) branched calls)
end

module Make_core (Sol : Solver.Mutable_incremental) = struct
  module Solver = struct
    include Solver.Mutable_to_pooled (Sol)

    let sat () =
      let res = Stats.As_ctx.add_time_of_to StatKeys.sat_time sat in
      Stats.As_ctx.incr StatKeys.sat_checks;
      if res = Unknown then Stats.As_ctx.incr StatKeys.sat_unknowns;
      res
  end

  module Solver_pool = struct
    let total_created () = Solver.total_resources ()
    let total_available () = Solver.available_resources ()
  end

  module Fuel = struct
    include Reversible.Make_effectful (Fuel_gauge)

    let consume_branching n = wrap (Fuel_gauge.consume_branching n) ()
    let consume_fuel_steps n = wrap (Fuel_gauge.consume_fuel_steps n) ()
    let take_branches list = wrap (Fuel_gauge.take_branches list) ()
  end

  module Value = Solver.Value
  module MONAD = Monad.IterM
  include MONAD
  module Flamegraph = Profiling.Flamegraph.Make (MONAD)

  module Give_up = struct
    type _ Effect.t += Gave_up_eff : string -> unit Effect.t

    let perform reason = Effect.perform (Gave_up_eff reason)

    let with_give_up_raising f =
      try f ()
      with effect Gave_up_eff reason, k ->
        let backtrace = Printexc.get_raw_backtrace () in
        Effect.Deep.discontinue_with_backtrace k (Gave_up reason) backtrace
  end

  type 'a t = 'a Iter.t

  type lfail = [ `Lfail of (Value.(sbool t)[@printer Value.ppa]) ]
  [@@deriving show { with_path = false }]

  type cons_fail = [ lfail | `Missing_subst of Var.t ]
  [@@deriving show { with_path = false }]

  module Symex_state = struct
    let backtrack_n n =
      Solver.backtrack_n n;
      Fuel.backtrack_n n;
      Flamegraph.backtrack_n n

    let save () =
      Solver.save ();
      Fuel.save ();
      Flamegraph.save ()

    let run ~init_fuel f =
      Solver.run @@ fun () ->
      Fuel.run ~init:init_fuel @@ fun () -> f ()
  end

  let signal_unexplored_branch =
    let msg =
      String.Interned.intern
        "At least one branch was dropped because of fuel. The program is not \
         fully verified."
    in
    fun kind ->
      Stats.As_ctx.incr StatKeys.unexplored_branches;
      if Approx.As_ctx.is_ox () then Terminal.Warn.warn_once msg;
      match kind with
      | `Step -> [%l.debug "Exhausted step fuel"]
      | `Branch -> [%l.debug "Exhausted branch fuel"]

  let consume_fuel_steps n f =
    match Fuel.consume_fuel_steps n with
    | Exhausted -> signal_unexplored_branch `Step
    | Not_exhausted ->
        Stats.As_ctx.add_int StatKeys.steps n;
        f ()

  (** [Solver.simplify] throws an effect to ask the solver for simplification.
      When the value is already a concrete boolean literal, we can skip the
      effect dispatch entirely.

      [simplified_bool] returns the simplified value together with an its
      to_bool. *)
  let[@inline] simplified_bool v =
    match Value.to_bool v with
    | Some _ as b -> (v, b)
    | None ->
        let v = Solver.simplify v in
        (v, Value.to_bool v)

  let assume learned f =
    let rec aux acc learned =
      match learned with
      | [] ->
          Solver.add_constraints acc;
          f ()
      | l :: ls -> (
          let l, to_bool = simplified_bool l in
          match to_bool with
          | Some true -> aux acc ls
          | Some false -> [%l.trace "Assuming false, stopping this branch"]
          | None -> aux (l :: acc) ls)
    in
    aux [] learned

  (** Same as {!assert_}, but not captured within the monad. Not to be exposed
      to the user, because without proper care, this could have unwanted
      side-effects at the wrong time. *)
  let assert_raw value : bool =
    let value, to_bool = simplified_bool value in
    match to_bool with
    | Some true -> true
    | Some false -> false
    | None ->
        let@ () =
          L.with_section (Fmt.str "Checking entailment for %a" Value.ppa value)
        in
        Symex_state.save ();
        Solver.add_constraints [ Value.(not value) ];
        let sat_result = Solver.sat () in
        let () =
          [%l.debug
            "Entailment SAT check returned %a" Solver_result.pp sat_result]
        in
        Symex_state.backtrack_n 1;
        if Approx.As_ctx.is_ux () then not (Solver_result.is_sat sat_result)
        else Solver_result.is_unsat sat_result

  (** Assert is [if%sat (not value) then error else ok]. In UX, assert only
      returns [false] if (not value) is {b satisfiable}. In OX, assert only
      returns [true] if (not value) is {b unsatisfiable}. *)
  let assert_ value f = f (assert_raw value)

  let nondet_UNSAFE ty =
    let v = Solver.fresh_var ty in
    Value.mk_var v ty

  let nondet ty f = f (nondet_UNSAFE ty)
  let simplify v f = f (Solver.simplify v)
  let fresh_var ty f = f (Solver.fresh_var ty)

  let branch_on ?(left_branch_name = "Left branch")
      ?(right_branch_name = "Right branch") guard ~(then_ : unit -> 'a t)
      ~(else_ : unit -> 'a t) : 'a t =
   fun f ->
    Stats.As_ctx.incr StatKeys.branch_on_calls;
    let guard, to_bool = simplified_bool guard in
    match to_bool with
    (* [then_] and [else_] could be ['a t] instead of [unit -> 'a t], if we
       remove the Some true and Some false optimisation. *)
    | Some true -> then_ () f
    | Some false -> else_ () f
    | None ->
        let left_unsat = ref false in
        Symex_state.save ();
        L.with_section ~is_branch:true left_branch_name (fun () ->
            Solver.add_constraints ~simplified:true [ guard ];
            let sat_res = Solver.sat () in
            left_unsat := Solver_result.is_unsat sat_res;
            if Solver_result.is_sat sat_res then then_ () f
            else [%l.trace "Branch is not feasible"]);
        Symex_state.backtrack_n 1;
        L.with_section ~is_branch:true right_branch_name (fun () ->
            Solver.add_constraints [ Value.(not guard) ];
            if !left_unsat then
              (* Right must be sat since left was not! We didn't branch so we
                 don't consume the counter *)
              else_ () f
            else (
              Stats.As_ctx.incr StatKeys.branch_on_branched;
              match Fuel.consume_branching 1 with
              | Exhausted -> signal_unexplored_branch `Branch
              | Not_exhausted ->
                  Stats.As_ctx.incr StatKeys.branches;
                  if Solver_result.is_sat (Solver.sat ()) then else_ () f
                  else [%l.trace "Branch is not feasible"]))

  let if_sure ?left_branch_name:_ ?right_branch_name:_ guard
      ~(then_ : unit -> 'a t) ~(else_ : unit -> 'a t) : 'a t =
   fun f ->
    let guard, to_bool = simplified_bool guard in
    match to_bool with
    (* [then_] and [else_] could be ['a t] instead of [unit -> 'a t], if we
       remove the Some true and Some false optimisation. *)
    | Some true -> then_ () f
    | Some false -> else_ () f
    | None ->
        Symex_state.save ();
        Solver.add_constraints ~simplified:true [ Value.not guard ];
        let neg_unsat = Solver_result.is_unsat (Solver.sat ()) in
        Symex_state.backtrack_n 1;
        if neg_unsat then (
          (* Adding this constraint is technically redundant, but it's still
             worth having it in the PC for simplifications. *)
          Solver.add_constraints ~simplified:true [ guard ];
          then_ () f)
        else
          (* Don't add anything to the PC: [else_] is the general fallback that
             must cover both cases (per the contract that [then_] and [else_]
             agree when [guard] holds). Constraining the PC either way would
             drop paths. *)
          else_ () f

  let branch_on_take_one_ux ?left_branch_name:_ ?right_branch_name:_ guard
      ~then_ ~else_ : 'a t =
   fun f ->
    let guard, to_bool = simplified_bool guard in
    match to_bool with
    | Some true -> then_ () f
    | Some false -> else_ () f
    | None ->
        Symex_state.save ();
        Solver.add_constraints ~simplified:true [ guard ];
        let left_sat = Solver_result.is_sat (Solver.sat ()) in
        if left_sat then then_ () f;
        Symex_state.backtrack_n 1;
        if not left_sat then (
          Solver.add_constraints [ Value.(not guard) ];
          else_ () f)

  let branch_on_take_one ?left_branch_name ?right_branch_name guard ~then_
      ~else_ f =
    if Approx.As_ctx.is_ux () then
      branch_on_take_one_ux ?left_branch_name ?right_branch_name guard ~then_
        ~else_ f
    else branch_on ?left_branch_name ?right_branch_name guard ~then_ ~else_ f

  let branches (brs : (unit -> 'a t) list) : 'a t =
   fun f ->
    let brs = Fuel.take_branches brs in
    (* If there are 0 or 1 branches, we don't do anything, else we add how many
       {new} branches we take. *)
    Stats.As_ctx.add_int StatKeys.branches (max (List.length brs - 1) 0);
    match brs with
    | [] -> ()
    | [ a ] -> a () f
    | a :: r ->
        (* First branch should not backtrack and last branch should not save *)
        let with_section =
          let branch_counter = ref 0 in
          fun f ->
            L.with_section ~is_branch:true
              ("Branch number " ^ string_of_int !branch_counter)
              (fun () ->
                f ();
                incr branch_counter)
        in
        let rec loop brs =
          match brs with
          | [ x ] ->
              Symex_state.backtrack_n 1;
              with_section @@ fun () -> x () f
          | x :: r ->
              Symex_state.backtrack_n 1;
              Symex_state.save ();
              (with_section @@ fun () -> x () f);
              loop r
          | [] -> failwith "unreachable"
        in
        Symex_state.save ();
        (with_section @@ fun () -> a () f);
        loop r

  let vanish () _f =
    Flamegraph.checkpoint ();
    ()

  let give_up reason _f =
    (* The bind ensures that the side effect will not be enacted before the
       whole process is ran. *)
    [%l.warn "Gave up: %s" reason];
    Stats.As_ctx.push_str StatKeys.give_up_reasons reason;
    let should_give_up =
      Approx.As_ctx.is_ox ()
      && Solver_result.admissible ~mode:OX (Solver.sat ())
    in
    Flamegraph.checkpoint ();
    if should_give_up then Give_up.perform reason

  let with_frame = Flamegraph.with_frame
end

module Base_extension (Core : Core) = struct
  open Core
  include Monad.Extend (Core)

  let assert_or_error guard err =
    branch_on
      Value.(not guard)
      ~then_:(fun () -> return (Compo_res.Error err))
      ~else_:(fun () -> return (Compo_res.Ok ()))

  let some_or_give_up reason = function
    | Some x -> return x
    | None -> give_up reason

  module Result = struct
    include Compo_res.T (Core)

    let miss_no_fix ~reason () =
      Stats.As_ctx.push_str StatKeys.miss_without_fix reason;
      [%l.debug "Missing without fix: %s" reason];
      miss []
  end

  module Syntax = struct
    include Monad.Make_syntax (Core)
    include Compo_res.Make_syntax (Result)

    module Symex_syntax = struct
      let branch_on = branch_on
      let branch_on_take_one = branch_on_take_one
      let if_sure = if_sure
    end
  end

  module Producer = struct
    module P =
      Monad.StateT_base
        (struct
          type t = Value.Expr.Subst.t option
        end)
        (Core)

    include P
    include Monad.Extend (P)

    module Syntax = struct
      include Syntax

      let ( let*^ ) x f = bind f (lift x)
      let ( let+^ ) x f = map f (lift x)

      module Symex_syntax = struct
        let[@inline] branch_on ?left_branch_name ?right_branch_name guard ~then_
            ~else_ =
         fun st ->
          branch_on ?left_branch_name ?right_branch_name guard
            ~then_:(fun () -> then_ () st)
            ~else_:(fun () -> else_ () st)

        let[@inline] branch_on_take_one ?left_branch_name ?right_branch_name
            guard ~then_ ~else_ =
         fun st ->
          branch_on_take_one ?left_branch_name ?right_branch_name guard
            ~then_:(fun () -> then_ () st)
            ~else_:(fun () -> else_ () st)

        let[@inline] if_sure ?left_branch_name ?right_branch_name guard ~then_
            ~else_ =
         fun st ->
          if_sure ?left_branch_name ?right_branch_name guard
            ~then_:(fun () -> then_ () st)
            ~else_:(fun () -> else_ () st)
      end
    end

    open Syntax

    let vanish () = lift (vanish ())

    let apply_subst (sf : (Value.Expr.t -> 'a Value.t) -> 'syn -> 'sem)
        (e : 'syn) : 'sem t =
     fun s ->
      (* There's maybe a safer version with effects and no reference? *)
      match s with
      | None ->
          let vsf e =
            let v, _ =
              let open Value.Expr.Subst in
              apply ~missing_var:(fun v ty -> Value.mk_var v ty) empty e
            in
            v
          in
          let res = sf vsf e in
          Core.return (res, None)
      | Some s ->
          let s = ref s in
          let vsf e =
            let v, s' =
              Value.Expr.Subst.apply
                ~missing_var:(fun _ ty -> nondet_UNSAFE ty)
                !s e
            in
            s := s';
            v
          in
          let res = sf vsf e in
          Core.return (res, Some !s)

    let produce_pure e : unit t =
      let is_bool = Value.is_bool_ty @@ Value.Expr.ty e in
      if not is_bool then (
        [%l.error
          "Producing non-boolean pure value!! This is quite probably a tool \
           bug, please report it. Expr: %a"
          Value.Expr.pp e];
        vanish ())
      else
        let* v = apply_subst Fun.id e in
        lift (assume [ v ])

    let run ~subst p =
      let ( let+ ) = Fun.flip Core.map in
      let+ x, s = p (Some subst) in
      (x, Option.get s)

    let run_identity p =
      let ( let+ ) = Fun.flip Core.map in
      let+ x, _s = p None in
      x

    let from_raw_UNSAFE x = x
  end

  module Consumer = struct
    type 'a symex = 'a t
    type subst = Value.Expr.Subst.t
    type ('a, 'fix) t = subst -> ('a * subst, cons_fail, 'fix) Result.t

    let lift_res (r : ('a, cons_fail, 'fix) Result.t) : ('a, 'fix) t =
     fun subst -> Result.map (fun a -> (a, subst)) r

    let lift (m : 'a symex) : ('a, 'fix) t =
     fun subst -> Core.map (fun a -> Compo_res.ok (a, subst)) m

    let branches (l : (unit -> ('a, 'fix) t) list) : ('a, 'fix) t =
     fun s -> branches (List.map (fun f () -> f () s) l)

    let ok x = fun subst -> Result.ok (x, subst)
    let lfail v = lift_res (Result.error (`Lfail v))
    let miss fixes = lift_res (Result.miss fixes)
    let miss_no_fix ~reason () = lift_res (Result.miss_no_fix ~reason ())

    let map (f : 'a -> 'b) (m : ('a, 'fix) t) : ('b, 'fix) t =
     fun s -> Result.map (fun (a, s) -> (f a, s)) (m s)

    let map_missing (f : 'fix -> 'g) (m : ('a, 'fix) t) : ('a, 'g) t =
     fun s -> Result.map_missing f (m s)

    let bind (f : 'a -> ('b, 'fix) t) (m : ('a, 'fix) t) : ('b, 'fix) t =
     fun s -> Result.bind (fun (a, s) -> f a s) (m s)

    let bind_res (f : ('a, cons_fail, 'fix) Compo_res.t -> ('b, 'fix2) t)
        (m : ('a, 'fix) t) : ('b, 'fix2) t =
     fun s ->
      Core.bind
        (function
          | Compo_res.Ok (a, s) -> f (Compo_res.Ok a) s
          | Error e -> f (Compo_res.Error e) s
          | Missing fixes -> f (Compo_res.Missing fixes) s)
        (m s)

    include Monad.Make_extension2 (struct
      type nonrec ('a, 'fix) t = ('a, 'fix) t

      let ok = ok
      let map = map
      let bind = bind
    end)

    let run ~subst p = p subst

    module Syntax = struct
      let ( let* ) x f = bind f x
      let ( let+ ) x f = map f x
      let ( let+? ) x f = map_missing f x
      let ( let*! ) x f = bind_res f x
      let ( let*^ ) x f = bind f (lift x)
      let ( let+^ ) x f = map f (lift x)

      module Symex_syntax = struct
        let[@inline] branch_on ?left_branch_name ?right_branch_name guard ~then_
            ~else_ =
         fun st ->
          branch_on ?left_branch_name ?right_branch_name guard
            ~then_:(fun () -> then_ () st)
            ~else_:(fun () -> else_ () st)

        let[@inline] branch_on_take_one ?left_branch_name ?right_branch_name
            guard ~then_ ~else_ =
         fun st ->
          branch_on_take_one ?left_branch_name ?right_branch_name guard
            ~then_:(fun () -> then_ () st)
            ~else_:(fun () -> else_ () st)

        let[@inline] if_sure ?left_branch_name ?right_branch_name guard ~then_
            ~else_ =
         fun st ->
          if_sure ?left_branch_name ?right_branch_name guard
            ~then_:(fun () -> then_ () st)
            ~else_:(fun () -> else_ () st)
      end
    end

    open Syntax

    let apply_subst (sf : (Value.Expr.t -> 'a Value.t) -> 'syn -> 'sem)
        (e : 'syn) : ('sem, 'fix) t =
      let exception Missing_subst of Var.t in
      fun s ->
        let vsf e =
          let v, _ =
            Value.Expr.Subst.apply
              ~missing_var:(fun v _ -> raise (Missing_subst v))
              s e
          in
          v
        in
        try
          let res = sf vsf e in
          Result.ok (res, s)
        with Missing_subst v -> Result.error (`Missing_subst v)

    let assert_pure v : (unit, 'fix) t =
      if Approx.As_ctx.is_ux () then lift (assume [ v ])
      else
        let*^ assert_passed = assert_ v in
        if assert_passed then ok () else lfail v

    let consume_pure e : (unit, 'fix) t =
      let* v = apply_subst Fun.id e in
      assert_pure v

    let learn_eq expr v : (unit, 'fix) t =
     fun subst ->
      match Value.Expr.Subst.learn subst expr v with
      | None ->
          [%l.debug "Couldn't learn %a := %a" Value.Expr.pp expr Value.ppa v];
          lfail (Value.of_bool false) subst
      | Some subst ->
          let v', subst =
            Value.Expr.Subst.apply
              ~missing_var:(fun _ _ ->
                tool_bug
                  "Tool Bug: learned substitution does not cover expression's \
                   free variables.")
              subst expr
          in
          assert_pure (Value.sem_eq_untyped v v') subst

    let expose_subst () : (subst, 'fix) t = fun subst -> Result.ok (subst, subst)
    let from_raw_UNSAFE x = x
  end
end

module Make (Sol : Solver.Mutable_incremental) :
  S with module Value = Sol.Value = struct
  (* TODO: CORE this can go away when `include functors` land
     (https://github.com/ocaml/ocaml/pull/14177) *)
  module CORE = Make_core (Sol)
  include CORE
  include Base_extension (CORE)

  (* TODO: with modular explicits this can be implemented for any module:
   * let manage (module M : Bookkeeping) = function
   *   | Ignore -> M.with_ignored ()
   *   | Dump arg -> M.with_dumped arg
   *   | Caller -> fun f -> f ()
   *)

  let manage_flamegraph = function
    | Ignore -> Flamegraph.with_ignored ()
    | Dump name -> Flamegraph.with_dumped name
    | Caller -> fun f -> f ()

  let manage_stats = function
    | Ignore -> Stats.As_ctx.with_ignored ()
    | Dump name -> Stats.As_ctx.with_dumped name
    | Caller -> fun f -> f ()

  let setup ?(flamegraph = Ignore) ?(stats = Ignore)
      ?(fuel = Fuel_gauge.infinite) ~mode f =
    let@ () = manage_flamegraph flamegraph in
    let@ () = manage_stats stats in
    let@ () = Stats.As_ctx.add_time_of_to StatKeys.exec_time in
    let@ () = Symex_state.run ~init_fuel:fuel in
    let@ () = Approx.As_ctx.with_mode mode in
    let@ () = Give_up.with_give_up_raising in
    f ()

  let run_iter ~mode (iter : 'a t) : ('a * Value.Expr.t list) Iter.t =
   fun continue ->
    (* Make sure to drop branches that have leftover assumes with unsatisfiable
       PCs. *)
    let admissible () = Solver_result.admissible ~mode (Solver.sat ()) in
    iter @@ fun x -> if admissible () then continue (x, Solver.as_exprs ())

  let run ?flamegraph ?stats ?fuel ~mode iter =
    let@ () = setup ?flamegraph ?stats ?fuel ~mode in
    Iter.to_list @@ run_iter ~mode iter

  module Result = struct
    include Result

    let run ?flamegraph ?stats ?fuel ?(fail_fast = false) ~mode iter =
      let@ () = setup ?flamegraph ?stats ?fuel ~mode in
      let l = ref [] in
      let () =
        let exception Fail_fast in
        try
          run_iter ~mode iter @@ fun (res, pc) ->
          let res = Compo_res.map_error (fun e -> Or_gave_up.E e) res in
          l := (res, pc) :: !l;
          if fail_fast && Compo_res.is_error res then raise Fail_fast
        with
        | effect Give_up.Gave_up_eff reason, k ->
            l := (Error (Gave_up reason), Solver.as_exprs ()) :: !l;
            if fail_fast then Effect.Deep.discontinue k Fail_fast
            else Effect.Deep.continue k ()
        | Fail_fast -> ()
      in
      List.rev !l
  end
end
